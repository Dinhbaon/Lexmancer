# Element generation with full schema enforcement
root ::= element

# Top-level structure
element ::= "{" ws "\"name\"" ws ":" ws string ws "," ws "\"description\"" ws ":" ws string ws "," ws "\"color\"" ws ":" ws hex-color ws "," ws "\"ability\"" ws ":" ws ability ws "}" ws

ability ::= "{" ws "\"primitives\"" ws ":" ws string-array ws "," ws "\"effects\"" ws ":" ws effect-array ws "," ws "\"cooldown\"" ws ":" ws number ws "," ws "\"description\"" ws ":" ws string ws "}" ws

effect-array ::= "[" ws effect-item (ws "," ws effect-item)* ws "]" ws | "[" ws "]" ws
effect-item ::= "{" ws "\"script\"" ws ":" ws action-array ws "}" ws

action-array ::= "[" ws top-action (ws "," ws top-action)* ws "]" ws | "[" ws "]" ws
top-action ::= spawn-melee | spawn-projectile | spawn-area | spawn-beam | chain-action | repeat-action

# Action definitions
spawn-melee ::= "{" ws "\"action\"" ws ":" ws "\"spawn_melee\"" ws "," ws "\"args\"" ws ":" ws melee-args ws (ws "," ws "\"on_hit\"" ws ":" ws on-hit-array ws)? (ws "," ws "\"on_expire\"" ws ":" ws on-hit-array ws)? "}" ws

melee-args ::= "{" ws "\"shape\"" ws ":" ws shape-value ws "," ws "\"range\"" ws ":" ws number ws (ws "," ws "\"arc_angle\"" ws ":" ws number ws)? (ws "," ws "\"width\"" ws ":" ws number ws)? (ws "," ws "\"windup_time\"" ws ":" ws number ws)? (ws "," ws "\"active_time\"" ws ":" ws number ws)? (ws "," ws "\"movement\"" ws ":" ws movement-value ws)? (ws "," ws "\"move_distance\"" ws ":" ws number ws)? (ws "," ws "\"move_duration\"" ws ":" ws number ws)? "}" ws

shape-value ::= "\"arc\"" ws | "\"circle\"" ws | "\"rectangle\"" ws

# Movement types: stationary (no move), dash (invulnerable dash with trail), lunge (leap then attack),
# jump_smash (leap then AOE), backstep (retreat), blink (instant teleport), teleport_strike (teleport to enemy)
movement-value ::= "\"stationary\"" ws | "\"dash\"" ws | "\"lunge\"" ws | "\"jump_smash\"" ws | "\"backstep\"" ws | "\"blink\"" ws | "\"teleport_strike\"" ws

spawn-projectile ::= "{" ws "\"action\"" ws ":" ws "\"spawn_projectile\"" ws "," ws "\"args\"" ws ":" ws projectile-args ws (ws "," ws "\"on_hit\"" ws ":" ws on-hit-array ws)? (ws "," ws "\"on_expire\"" ws ":" ws on-hit-array ws)? "}" ws

projectile-args ::= "{" ws "\"count\"" ws ":" ws number ws "," ws "\"speed\"" ws ":" ws number ws (ws "," ws "\"pattern\"" ws ":" ws pattern-value ws)? (ws "," ws "\"acceleration\"" ws ":" ws number ws)? (ws "," ws "\"piercing\"" ws ":" ws boolean ws)? (ws "," ws "\"max_pierce_hits\"" ws ":" ws number ws)? "}" ws

pattern-value ::= "\"single\"" ws | "\"spread\"" ws | "\"spiral\"" ws | "\"circle\"" ws

spawn-area ::= "{" ws "\"action\"" ws ":" ws "\"spawn_area\"" ws "," ws "\"args\"" ws ":" ws area-args ws (ws "," ws "\"on_hit\"" ws ":" ws on-hit-array ws)? (ws "," ws "\"on_expire\"" ws ":" ws on-hit-array ws)? "}" ws

area-args ::= "{" ws "\"radius\"" ws ":" ws number ws "," ws "\"duration\"" ws ":" ws number ws (ws "," ws "\"lingering_damage\"" ws ":" ws number ws)? (ws "," ws "\"growth_time\"" ws ":" ws number ws)? (ws "," ws "\"damage\"" ws ":" ws number ws)? "}" ws

spawn-beam ::= "{" ws "\"action\"" ws ":" ws "\"spawn_beam\"" ws "," ws "\"args\"" ws ":" ws beam-args ws (ws "," ws "\"on_hit\"" ws ":" ws on-hit-array ws)? (ws "," ws "\"on_expire\"" ws ":" ws on-hit-array ws)? "}" ws

beam-args ::= "{" ws "\"length\"" ws ":" ws number ws "," ws "\"width\"" ws ":" ws number ws "," ws "\"duration\"" ws ":" ws number ws (ws "," ws "\"travel_time\"" ws ":" ws number ws)? "}" ws

chain-action ::= "{" ws "\"action\"" ws ":" ws "\"chain_to_nearby\"" ws "," ws "\"args\"" ws ":" ws chain-args ws (ws "," ws "\"on_hit\"" ws ":" ws on-hit-array ws)? "}" ws

chain-args ::= "{" ws "\"max_chains\"" ws ":" ws number ws "," ws "\"range\"" ws ":" ws number ws "}" ws

repeat-action ::= "{" ws "\"action\"" ws ":" ws "\"repeat\"" ws "," ws "\"args\"" ws ":" ws repeat-args ws (ws "," ws "\"on_hit\"" ws ":" ws on-hit-array ws)? "}" ws

repeat-args ::= "{" ws "\"count\"" ws ":" ws number ws "," ws "\"interval\"" ws ":" ws number ws "}" ws

# On-hit actions
on-hit-array ::= "[" ws on-hit-item (ws "," ws on-hit-item)* ws "]" ws | "[" ws "]" ws
on-hit-item ::= damage-action | heal-action | apply-status | knockback-action | chain-action | spawn-melee | spawn-projectile | spawn-area | spawn-beam

damage-action ::= "{" ws "\"action\"" ws ":" ws "\"damage\"" ws "," ws "\"args\"" ws ":" ws damage-args ws "}" ws

damage-args ::= "{" ws "\"amount\"" ws ":" ws number ws (ws "," ws "\"element\"" ws ":" ws element-value ws)? (ws "," ws "\"area_radius\"" ws ":" ws number ws)? "}" ws

element-value ::= "\"fire\"" ws | "\"water\"" ws | "\"earth\"" ws | "\"lightning\"" ws | "\"poison\"" ws | "\"wind\"" ws | "\"shadow\"" ws | "\"light\"" ws | "\"neutral\"" ws

heal-action ::= "{" ws "\"action\"" ws ":" ws "\"heal\"" ws "," ws "\"args\"" ws ":" ws heal-args ws "}" ws

heal-args ::= "{" ws "\"amount\"" ws ":" ws number ws "}" ws

apply-status ::= "{" ws "\"action\"" ws ":" ws "\"apply_status\"" ws "," ws "\"args\"" ws ":" ws status-args ws "}" ws

status-args ::= "{" ws "\"status\"" ws ":" ws status-value ws "," ws "\"duration\"" ws ":" ws number ws (ws "," ws "\"stacks\"" ws ":" ws boolean ws)? "}" ws

status-value ::= "\"burning\"" ws | "\"frozen\"" ws | "\"poisoned\"" ws | "\"shocked\"" ws | "\"slowed\"" ws | "\"stunned\"" ws | "\"weakened\"" ws | "\"feared\"" ws

knockback-action ::= "{" ws "\"action\"" ws ":" ws "\"knockback\"" ws "," ws "\"args\"" ws ":" ws knockback-args ws "}" ws

knockback-args ::= "{" ws "\"force\"" ws ":" ws number ws "," ws "\"direction\"" ws ":" ws direction-value ws "}" ws

direction-value ::= "\"away\"" ws | "\"towards\"" ws | "\"towards_center\"" ws

# JSON primitives
hex-color ::= "\"#" [0-9A-Fa-f] [0-9A-Fa-f] [0-9A-Fa-f] [0-9A-Fa-f] [0-9A-Fa-f] [0-9A-Fa-f] "\"" ws

string-array ::= "[" ws string-item (ws "," ws string-item)* ws "]" ws | "[" ws "]" ws
string-item ::= string ws

string ::= "\"" char* "\""
char ::= [^"\\\r\n] | "\\" ["\\/bfnrt]

number ::= "-"? ("0" | [1-9] [0-9]*) ("." [0-9]+)? ([eE] [-+]? [0-9]+)?

boolean ::= "true" | "false"

ws ::= [ \t\n\r]*
